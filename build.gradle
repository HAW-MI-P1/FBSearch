apply plugin: "java"
apply plugin: "maven"

group = "myorg"
version = 1.0

repositories {
    mavenCentral()
}

sourceSets.all { set ->
    def jarTask = task("${set.name}Jar", type: Jar) {
        baseName = baseName + "-$set.name"
        from set.output
    }

    artifacts {
        archives jarTask
    }
}

sourceSets {
    api
    impl
}

dependencies {
    apiCompile 'commons-codec:commons-codec:1.5'

    implCompile sourceSets.api.output
    implCompile 'commons-lang:commons-lang:2.6'
    implCompile 'edu.stanford.nlp:stanford-corenlp:3.5.0'
    implCompile 'edu.stanford.nlp:stanford-corenlp:3.5.0:models'	
	
    testCompile 'junit:junit:4.9'
    testCompile sourceSets.api.output
    testCompile sourceSets.impl.output
    runtime configurations.apiRuntime
    runtime configurations.implRuntime

    compile 'org.json:json:20090211'

    compile 'org.apache.httpcomponents:httpclient:4.0-alpha4'
    compile "org.apache.httpcomponents:httpmime:4.2.3"

  
    compile 'edu.stanford.nlp:stanford-corenlp:3.5.0'
    compile 'edu.stanford.nlp:stanford-corenlp:3.5.0:models'

	compile 'org.apache.jena:jena-arq:2.12.1'

    compile 'org.xerial:sqlite-jdbc:3.8.7'
}

//forces gradle to use xml-apis:xml-apis:1.4.01 instead of the older xml-apis:xml-apis:2.0.2 (somebody should get punished for this!)
//http://forums.gradle.org/gradle/topics/oddity_in_the_output_of_dependencyinsight_task
//http://mvnrepository.com/artifact/xml-apis/xml-apis
configurations.all {
	resolutionStrategy {
	    force 'xml-apis:xml-apis:1.4.01'
	}
}

jar {
    from sourceSets.api.output
    from sourceSets.impl.output
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri("${buildDir}/repo"))

            addFilter("main") { artifact, file -> artifact.name == project.name }
            ["api", "impl"].each { type ->
                addFilter(type) { artifact, file -> artifact.name.endsWith("-$type") }
                
                // We now have to map our configurations to the correct maven scope for each pom
                ["compile", "runtime"].each { scope ->
                    configuration = configurations[type + scope.capitalize()]
                    ["main", type].each { pomName ->
                        pom(pomName).scopeMappings.addMapping 1, configuration, scope
                    }
                }
            }

        }
    }
}
